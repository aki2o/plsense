#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long;
use IO::Socket;
use File::Spec;
use PPI::Lexer;
use PPI::Dumper;
use Try::Tiny;
use PlSense::Logger;
use PlSense::SocketClient;
use PlSense::Builtin;
use PlSense::ModuleKeeper;
use PlSense::AddressRouter;
use PlSense::SubstituteValueFinder;
use PlSense::CodeAssist;
use PlSense::Helper;

my %opthelp_of = ("-h, --help"     => "Show this message.",
                  "-c, --cachedir" => "Path of directory caching information for Completion/Help.",
                  "--port1"        => "Port number for listening by main server process. Default is 33333.",
                  "--port2"        => "Port number for listening by work server process. Default is 33334.",
                  "--port3"        => "Port number for listening by resolve server process. Default is 33335.",
                  "--loglevel"     => "Level of logging. Its value is for Log::Handler.",
                  "--logfile"      => "Path of log file.",
                  );

my %function_of = (status        => \&get_status,
                   pid           => \&get_own_pid,
                   init          => \&initialize,
                   remove        => \&remove,
                   removeall     => \&remove_all,
                   removeprojall => \&remove_project_all,
                   modhelp       => \&help_module,
                   subhelp       => \&help_method,
                   varhelp       => \&help_variable,
                   assisthelp    => \&help_last_assist,
                   codehelp      => \&help_code,
                   subinfo       => \&get_method_information,
                   onproj        => \&set_currentproject,
                   onfile        => \&set_currentfile,
                   onmod         => \&set_currentmodule,
                   onsub         => \&set_currentmethod,
                   location      => \&get_current_location,
                   codeassist    => \&assist_coding,
                   ready         => \&is_ready,
                   debugstk      => \&debug_stocked,
                   debugmod      => \&debug_module,
                   debugrt       => \&debug_routing,
                   debuglex      => \&debug_lexer,
                   uninit        => \&uninitialize_module,
                   builtin       => \&setup_builtin,
                   found         => \&setup_found_module,
                   built         => \&setup_built_module,
                   resolved      => \&setup_resolved,
                   );

my ($cachedir, $port1, $port2, $port3, $loglvl, $logfile);
GetOptions ('help'       => sub { show_usage(); exit 0; },
            'cachedir=s' => \$cachedir,
            'port1=i'    => \$port1,
            'port2=i'    => \$port2,
            'port3=i'    => \$port3,
            'loglevel=s' => \$loglvl,
            'logfile=s'  => \$logfile,
            );

setup_logger($loglvl, $logfile);
if ( ! $cachedir || ! -d $cachedir ) {
    logger->fatal("Not exist cache directory [$cachedir]");
    exit 1;
}

my $scli = PlSense::SocketClient->new({ mainport => $port1,
                                        workport => $port2,
                                        resolveport => $port3,
                                        retryinterval => 0.2,
                                        maxretry => 5 });

my $sock = IO::Socket::INET->new(LocalAddr => "localhost",
                                 LocalPort => $scli->get_mainport,
                                 Proto => "tcp",
                                 Listen => 1,
                                 ReUse => 1,
                                 );
if ( ! $sock ) {
    logger->fatal("Can't create socket : $!");
    exit 1;
}
if ( ! $sock->listen ) {
    logger->fatal("Can't listening port [$port1] : $!");
    exit 1;
}

my $builtin = PlSense::Builtin->new({ cachedir => $cachedir });
my $mdlkeeper = PlSense::ModuleKeeper->new({ cachedir => $cachedir });
my $addrrouter = PlSense::AddressRouter->new({ cachedir => $cachedir,
                                               mdlkeeper => $mdlkeeper,
                                               with_build => 0, });
my $addrfinder = PlSense::SubstituteValueFinder->new({ builtin => $builtin,
                                                       mdlkeeper => $mdlkeeper,
                                                       with_build => 0, });
my $assist = PlSense::CodeAssist->new({ builtin => $builtin,
                                        mdlkeeper => $mdlkeeper,
                                        addrrouter => $addrrouter,
                                        addrfinder => $addrfinder });
my $helper = PlSense::Helper->new({ addrrouter => $addrrouter, addrfinder => $addrfinder });
my $currfilepath = "";
my $currmdlnm = "";
my $currmtdnm = "";

$SIG{WINCH} = sub { logger->notice("Receive SIGWINCH"); exit 0; };
$SIG{TERM} = sub { logger->notice("Receive SIGTERM"); exit 0; };
$SIG{USR1} = sub { logger->notice("Receive SIGUSR1"); $addrrouter->reset; accept_client(); };
$SIG{HUP} = sub { logger->notice("Receive SIGHUP"); $addrrouter->reset; accept_client(); };

accept_client();
exit 0;


sub show_usage {
    my $optstr = "";
    OPTHELP:
    foreach my $key ( sort keys %opthelp_of ) {
        $optstr .= sprintf("  %-25s %s\n", $key, $opthelp_of{$key});
    }

    print <<"EOF";
Run PlSense Main Server.
Main Server handles request about Completion/Help.

Usage:
  plsense-server-main [Option]

Option:
$optstr
EOF
    return;
}

sub accept_client {
    logger->info("Starting main server");

    initialize();
    ACCEPT_CLIENT:
    while ( my $cli = $sock->accept ) {
        logger->debug("Waiting client ...");

        my $line = $cli->getline || "";
        chomp $line;
        logger->info("Receive request : $line");
        my $cmdnm = $line =~ s{ ^ \s* ([a-z]+) }{}xms ? $1 : "";
        if ( $cmdnm eq "quit" ) {
            $cli->close;
            next ACCEPT_CLIENT;
        }
        elsif ( $cmdnm eq "stop" ) {
            $cli->close;
            last ACCEPT_CLIENT;
        }
        elsif ( exists $function_of{$cmdnm} ) {
            try {
                my $fnc = $function_of{$cmdnm};
                my $ret = &$fnc($line) || "";
                $cli->print($ret);
                $cli->flush;
            }
            catch {
                my $e = shift;
                logger->error("Failed do $cmdnm : $e");
            };
        }
        else {
            logger->error("Unknown command [$cmdnm]");
        }
        $cli->close;

    }
    $sock->close;

    logger->info("Stopping main server");
}

sub initialize {
    $addrrouter->reload_current_project;
    return;
}



sub get_status {
    return "Running\n";
}

sub get_own_pid {
    return $$."\n";
}

sub remove {
    my $mdl_or_files = shift || "";
    my @mdl_or_files = split m{ \| }xms, $mdl_or_files;
    ENTRY:
    foreach my $mdl_or_file ( @mdl_or_files ) {
        $mdl_or_file =~ s{ ^\s+ }{}xms;
        $mdl_or_file =~ s{ \s+$ }{}xms;
        if ( ! $mdl_or_file ) { next ENTRY; }
        my ($mdlnm, $filepath) = ("", "");
        if ( -f $mdl_or_file ) {
            $mdlnm = "main";
            $filepath = File::Spec->rel2abs($mdl_or_file);
        }
        else {
            $mdlnm = $mdl_or_file;
        }
        my $mdl = $mdlkeeper->get_module($mdlnm, $filepath) or next ENTRY;
        if ( $filepath ) {
            foreach my $m ( $mdlkeeper->get_bundle_modules($filepath) ) {
                $mdlkeeper->remove_module($m->get_name, $m->get_filepath, $m->get_projectnm);
            }
        }
        else {
            $mdlkeeper->remove_module($mdlnm, $filepath, $mdl->get_projectnm);
        }
    }
    return "Done\n";
}

sub remove_all {
    $mdlkeeper->remove_all_module;
    initialize();
    return "Done\n";
}

sub remove_project_all {
    $mdlkeeper->remove_project_all_module;
    return "Done\n";
}

sub help_module {
    my $mdlnm = shift || "";
    $mdlnm =~ s{ ^\s+ }{}xms;
    $mdlnm =~ s{ \s+$ }{}xms;
    my $mdl = $mdlkeeper->get_module($mdlnm);
    if ( ! $mdl ) {
        logger->error("Not found module[$mdlnm]");
        return;
    }
    return $helper->get_symbol_help_text($mdl);
}

sub help_method {
    my $arg = shift || "";
    $arg =~ s{ ^\s+ }{}xms;
    my @e = split m{ \s+ }xms, $arg;
    my $mtdnm = shift @e || "";
    my $mdlnm = shift @e || "";
    my $mdl = $mdlkeeper->get_module($mdlnm);
    my $mtd = $mdl ? $mdl->get_any_method($mtdnm) : undef;
    if ( $mtd ) { return $helper->get_symbol_help_text($mtd); }
    if ( $builtin->exist_method($mtdnm) ) {
        my $mtd = $builtin->get_method($mtdnm);
        return $helper->get_symbol_help_text($mtd);
    }
    my $fullnm = $mdlnm ? $mdlnm."::".$mtdnm : $mtdnm;
    logger->error("Not found method[$fullnm]");
    return;
}

sub help_variable {
    my $arg = shift || "";
    $arg =~ s{ ^\s+ }{}xms;
    my @e = split m{ \s+ }xms, $arg;
    my $varnm = shift @e || "";
    my $mdlnm = shift @e || "";
    my $mdl = $mdlkeeper->get_module($mdlnm);
    my $var = $mdl && $mdl->exist_member($varnm) ? $mdl->get_member($varnm) : undef;
    if ( $var ) { return $helper->get_symbol_help_text($var); }
    if ( $builtin->exist_variable($varnm) ) {
        my $var = $builtin->get_variable($varnm);
        return $helper->get_symbol_help_text($var);
    }
    my $fullnm = $mdlnm ? $mdlnm."::".$varnm : $varnm;
    logger->error("Not found variable[$fullnm]");
    return;
}

sub help_last_assist {
    my $candidate = shift || "";
    $candidate =~ s{ ^\s+ }{}xms;
    $candidate =~ s{ \s+$ }{}xms;
    my $any = $assist->get_last_candidate_instance($candidate);
    return $helper->get_any_help_text($any);
}

sub help_code {
    my $code = shift || "";
    update_instance_condition() or return;
    return $helper->get_help_text_by_code($code);
}

sub get_method_information {
    my $code = shift || "";
    update_instance_condition() or return;
    return $helper->get_method_info_by_code($code);
}

sub set_currentproject {
    my $projnm = shift || "";
    $projnm =~ s{ ^\s+ }{}xms;
    $projnm =~ s{ \s+$ }{}xms;
    if ( ! $projnm ) { $projnm = $mdlkeeper->get_default_project_name; }
    $mdlkeeper->switch_project($projnm);
    $addrrouter->switch_project($projnm);
    return "Done\n";
}

sub set_currentfile {
    my $filepath = shift || "";
    $filepath =~ s{ ^\s+ }{}xms;
    $filepath =~ s{ \s+$ }{}xms;
    if ( ! -f $filepath ) {
        logger->error("Not exist file[$filepath]");
        return "Failed\n";
    }
    $currfilepath = File::Spec->rel2abs($filepath);
    $currmdlnm = "main";
    $currmtdnm = "";
    logger->notice("Set current file is [$currfilepath]");
    return "Done\n";
}

sub set_currentmodule {
    my $mdlnm = shift || "";
    $mdlnm =~ s{ ^\s+ }{}xms;
    $mdlnm =~ s{ \s+$ }{}xms;
    $currmdlnm = $mdlnm;
    $currmtdnm = "";
    logger->notice("Set current module is [$currmdlnm]");
    return "Done\n";
}

sub set_currentmethod {
    my $mtdnm = shift || "";
    $mtdnm =~ s{ ^\s+ }{}xms;
    $mtdnm =~ s{ \s+$ }{}xms;
    $currmtdnm = $mtdnm;
    logger->notice("Set current method is [$currmtdnm]");
    return "Done\n";
}

sub get_current_location {
    my $ret = "Project: ".$mdlkeeper->get_project."\n";
    $ret .= "File: $currfilepath\n";
    $ret .= "Module: $currmdlnm\n";
    $ret .= "Sub: $currmtdnm\n";
    return $ret;
}

sub assist_coding {
    my $code = shift || "";
    update_instance_condition() or return;
    return join("\n", $assist->get_assist($code))."\n";
}

sub is_ready {
    my $mdl_or_file = shift || "";
    $mdl_or_file =~ s{ ^\s+ }{}xms;
    $mdl_or_file =~ s{ \s+$ }{}xms;
    my ($mdlnm, $filepath) = ("", "");
    if ( ! $mdl_or_file ) {
        return join("\n", map { $_->get_name } $mdlkeeper->get_built_modules)."\n";
    }
    elsif ( -f $mdl_or_file ) {
        $mdlnm = "main";
        $filepath = File::Spec->rel2abs($mdl_or_file);
    }
    else {
        $mdlnm = $mdl_or_file;
    }
    my $mdl = $mdlkeeper->get_module($mdlnm, $filepath);
    if ( ! $mdl ) {
        return "Not Found\n";
    }
    if ( ! $mdl->is_initialized ) { return "No\n"; }
    return "Yes\n";
}

sub debug_stocked {
    my $ret = $mdlkeeper->describe_keep_value;
    $ret .= $addrrouter->describe_keep_value;
    return $ret;
}

sub debug_module {
    my $mdl_or_file = shift || "";
    $mdl_or_file =~ s{ ^\s+ }{}xms;
    $mdl_or_file =~ s{ \s+$ }{}xms;
    my ($mdlnm, $filepath) = ("", "");
    if ( -f $mdl_or_file ) {
        $mdlnm = "main";
        $filepath = File::Spec->rel2abs($mdl_or_file);
    }
    else {
        $mdlnm = $mdl_or_file;
    }
    my $mdl = $mdlkeeper->get_module($mdlnm, $filepath) or return;
    return $mdl->to_detail_string;
}

sub debug_routing {
    my $regexp = shift || "";
    return $addrrouter->to_string_by_regexp($regexp);
}

sub debug_lexer {
    my $code = shift || "";
    my $lexer = PPI::Lexer->new();
    my $doc = $lexer->lex_source($code) or return;
    my $dumper = PPI::Dumper->new($doc) or return;
    return $dumper->string;
}

sub uninitialize_module {
    my $mdl_or_file = shift || "";
    $mdl_or_file =~ s{ ^\s+ }{}xms;
    $mdl_or_file =~ s{ \s+$ }{}xms;
    my ($mdlnm, $filepath) = ("", "");
    if ( -f $mdl_or_file ) {
        $mdlnm = "main";
        $filepath = File::Spec->rel2abs($mdl_or_file);
    }
    else {
        $mdlnm = $mdl_or_file;
    }
    my $mdl = $mdlkeeper->get_module($mdlnm, $filepath) or return;
    if ( $filepath ) {
        foreach my $m ( $mdlkeeper->get_bundle_modules($filepath) ) {
            $m->uninitialized;
        }
    }
    else {
        $mdl->uninitialized;
    }

    return;
}

sub setup_builtin {
    $builtin->load;
    return;
}

sub setup_found_module {
    my $mdl_or_files = shift || "";
    my @mdl_or_files = split m{ \| }xms, $mdl_or_files;
    ENTRY:
    foreach my $mdl_or_file ( @mdl_or_files ) {
        $mdl_or_file =~ s{ ^\s+ }{}xms;
        $mdl_or_file =~ s{ \s+$ }{}xms;
        if ( ! $mdl_or_file ) { next ENTRY; }
        my ($mdlnm, $filepath) = ("", "");
        if ( -f $mdl_or_file ) {
            $mdlnm = "main";
            $filepath = File::Spec->rel2abs($mdl_or_file);
        }
        else {
            $mdlnm = $mdl_or_file;
        }
        $mdlkeeper->get_module($mdlnm, $filepath);
    }
    logger->info("Finished setup found module");
    return;
}

sub setup_built_module {
    my $mdl_or_files = shift || "";
    my @mdl_or_files = split m{ \| }xms, $mdl_or_files;
    ENTRY:
    foreach my $mdl_or_file ( @mdl_or_files ) {
        $mdl_or_file =~ s{ ^\s+ }{}xms;
        $mdl_or_file =~ s{ \s+$ }{}xms;
        if ( ! $mdl_or_file ) { next ENTRY; }
        my ($mdlnm, $filepath) = ("", "");
        if ( -f $mdl_or_file ) {
            $mdlnm = "main";
            $filepath = $mdl_or_file;
        }
        else {
            $mdlnm = $mdl_or_file;
        }
        my $mdl = $mdlkeeper->load_module($mdlnm, $filepath) or next ENTRY;
        logger->notice("Finished reload module of '$mdl_or_file'");
    }
    return;
}

sub setup_resolved {
    $addrrouter->reload_current_project;
    return;
}

sub update_instance_condition {
    my $currmdl = $addrfinder->get_currentmodule;
    if ( ! $currmdl || $currmdl->get_name ne $currmdlnm || $currmdl->get_filepath ne $currfilepath ) {
        my $mdl = $mdlkeeper->get_module($currmdlnm, $currfilepath);
        if ( ! $mdl ) {
            if ( ! $currmdlnm ) {
                logger->error("Not yet set current file/module by onfile/onmod command");
            }
            else {
                logger->error("Not yet exist [$currmdlnm] of [$currfilepath]");
                logger->error("Check the module status is not 'Nothing' by ready command.");
            }
            return 0;
        }
        $addrfinder->set_currentmodule($mdl);
        $currmdl = $mdl;
    }
    my $currmtd = $addrfinder->get_currentmethod;
    if ( $currmtdnm ) {
        if ( ! $currmtd || $currmtd->get_name ne $currmtdnm ) {
            my $mtd = $currmdl->get_method($currmtdnm);
            if ( ! $mtd ) {
                logger->error("Not found [$currmtdnm] in [$currmdlnm] of [$currfilepath]");
                logger->error("Check the module status is 'Yes' by ready command.");
                return 0;
            }
            $addrfinder->set_currentmethod($mtd);
        }
    }
    elsif ( $currmtd ) {
        $addrfinder->init_currentmethod;
    }
    return 1;
}


